# Requires Skript-Reflect, Skript, FancyNpcs, and FancyHolograms
# doesnt have everything, but some of the most important elements.

# this is the function version, which is not as simple to use but cleaner and more efficient

import:
    de.oliver.fancynpcs.api.FancyNpcsPlugin
    de.oliver.fancynpcs.api.Npc
    de.oliver.fancynpcs.api.NpcData
    de.oliver.fancynpcs.api.utils.SkinFetcher

    de.oliver.fancyholograms.api.FancyHologramsPlugin
    de.oliver.fancyholograms.api.HologramManager
    de.oliver.fancyholograms.api.data.HologramData
    de.oliver.fancyholograms.api.hologram.Hologram
    
    java.util.List

# FancyHolograms

function setHologramText(h: string, t: string):
    set {_manager} to FancyHologramsPlugin.get().getHologramManager()
    set {_hologram} to {_manager}.getHologram({_h}).orElse(null)    
    
    set {_hologramData} to {_hologram}.getData()
    {_hologramData}.setText(List.of({_t}))

function addHologramLine(h: string, t: string):
    set {_manager} to FancyHologramsPlugin.get().getHologramManager()
    set {_hologram} to {_manager}.getHologram({_h}).orElse(null)    
    
    set {_hologramData} to {_hologram}.getData()
    {_hologramData}.addLine({_t})

function removeHologramLine(h: string, l: number):
    set {_manager} to FancyHologramsPlugin.get().getHologramManager()
    set {_hologram} to {_manager}.getHologram({_h}).orElse(null)    
    
    set {_hologramData} to {_hologram}.getData()
    {_hologramData}.removeLine({_l})

function updateHoogram(h: string):
    set {_manager} to FancyHologramsPlugin.get().getHologramManager()
    set {_hologram} to {_manager}.getHologram({_h}).orElse(null)    
    
    {_hologram}.queueUpdate()

# FancyHolograms Getters

function getHologramText(h: string) :: string:
    set {_manager} to FancyHologramsPlugin.get().getHologramManager()
    set {_hologram} to {_manager}.getHologram({_h}).orElse(null)    
    
    return {_hologram}.getData().getText()

# FancyNpcs

function setNpcSkin(n: string, s: string):
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})

    set {_skinData} to new SkinFetcher.SkinData({_s}, null, null)
    {_npc}.getData().setSkin({_skinData})

    {_npc}.removeForAll()
    {_npc}.create()
    {_npc}.spawnForAll()

function setNpcName(n: string, s: string):
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    {_npc}.getData().setDisplayName({_s})

    {_npc}.updateForAll()

function setNpcLocation(n: string, l: location):
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    {_npc}.getData().setLocation({_l})

    {_npc}.updateForAll()

function setNpcScale(n: string, s: number):
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    {_npc}.getData().setScale({_s})

    {_npc}.updateForAll()

# FancyNpcs Getters

function getNpcLocation(n: string) :: location:
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    return {_npc}.getData().getLocation()

function getNpcName(n: string) :: string:
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    return {_npc}.getData().getDisplayName()

function getNpcId(n: string) :: string:
    set {_npc} to FancyNpcsPlugin.get().getNpcManager().getNpc({_n})
    return {_npc}.getData().getId()